<!-- 
Copyright 1998 Jason Abbott (jabbott@uidaho.edu)
Last updated 05/19/98
-->

<html>
<body link="#800000" vlink="#800000" alink="#E4C721" bgcolor="#FFFFFF">

<%
if Session("user") = "guest" then response.redirect "./error.asp"

dim db, query

' UPDATE DATABASE

Set db = Server.CreateObject("ADODB.Connection")
db.Open "bc"
query = "INSERT INTO helpdesk (" _
	& "submitter, " _
	& "category, " _
	& "description, " _
	& "submit_time, " _
	& "submit_machine, " _
	& "status" _
	& ") VALUES ('" _
	& Request.Form("submitter") & "', '" _
	& Request.Form("category") & "', '" _
	& replace(Request.Form("description"), "'", "’") & "', '" _
	& Now & "', '" _
	& Request.ServerVariables("REMOTE_ADDR") & "', '" _
	& "open')"

db.Execute(query)

' NOTIFY TECHNICIANS BY MAIL

query = "SELECT * FROM employee WHERE " _
	& "email_name = '" & Request.Form("submitter") & "'"

set rs = db.Execute(query)

Set mailer = Server.CreateObject("SMTPsvg.Mailer")
mailer.FromName = rs("name_first") & " " & rs("name_last")
mailer.FromAddress = rs("email_name") & "@" & rs("email_site")
mailer.RemoteHost = "borah.engboi.uidaho.edu"
mailer.AddRecipient "Jason Abbott","jabbott@uidaho.edu"
mailer.AddRecipient "Diane Griffitts","dianeg@uidaho.edu"
mailer.AddRecipient "Norm Galey","ngaley@uidaho.edu"
mailer.AddCC rs("name_first") & " " & rs("name_last"), rs("email_name") & "@" & rs("email_site")
mailer.Subject = "Help Request"

body = "You have been asked to help with the following situation:" _
	& VbCrLf & VbCrLf _
	& "       Name: " & rs("name_first") & " " & rs("name_last") _
	& VbCrLf _
	& "   Category: " & Request.Form("category") _
	& VbCrLf _
	& "Description: "

if Request.Form("description") = "" then
	body = body & rs("name_first") & " did not enter a description."
else
	body = body & Request.Form("description")
end if

body = body & VbCrLf & VbCrLf & "This message was generated by the Boise Center Help Desk.  Please visit http://boise.uidaho.edu/help to submit a new request or review past or current requests."

mailer.BodyText = body

mailer.SMTPLog = "c:\internet\mail\smtp.log"
if Mailer.SendMail then
%>
You request has been entered into the HelpDesk database and e-mail has been sent to the Boise Center technicians.  You should be receiving a copy of this e-mail.  If you do not, please check your <a href="./employee/detail.asp?email_name=<%=rs("email_name")%>">personal configuration</a> to confirm that your e-mail address is entered correctly.

<% else %>
	<!--#include file="smtp_log.inc"-->
<%
end if
rs.Close
db.Close
%>

</body>
</html>