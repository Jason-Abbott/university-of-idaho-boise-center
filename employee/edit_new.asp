<!-- 
Copyright 1999 Jason Abbott (jabbott@uidaho.edu)
Last updated 8/25/98
-->

<%
dim fill, db, rs, form, i, college, college_list

' must use session variable here because the SQL is
' generated by cycling through the form variables
' which can't include employee_type

if Request.QueryString("action") = "add" then

 	form = "added"
 	Session("type") = Request.QueryString("type")
	college = Request.QueryString("college")

' the fill variable determines whether the form
' should be populated with data
	
   fill = 0
	title = "Add new " & college & " " & Session("type") & " member"
else
   form = "updated"
   fill = 1
   
   Set db = Server.CreateObject("ADODB.Connection")
   db.Open "bc"
   query = "SELECT * FROM employee WHERE email_name = '" & Request.QueryString("email_name") & "'"
   Set rs = db.Execute(query)
	
	Session("type") = rs("type")
	college = rs("college")
	title = Request.QueryString("action") & " " & rs("name_first") & " " & rs("name_last")
end if

' create an array of college choices

college_list = array( _
	"Agriculture", _
	"Engineering", _
	"Education", _
	"Forestry", _
	"Law", _
	"Letters and Sciences", _
	"Other")
%>

<!--#include virtual="/header_start.inc"-->
<%=title%>
<!--#include virtual="/header_end.inc"-->

<!-- DELETION FORM --------------------------------------->

<% if Request.QueryString("action") = "delete" then %>

<form action="deleted.asp" method="post">

<font size=5>
Press "Delete" if you are sure that you want to delete
<b><%=rs("name_first") & " " & rs("name_last")%></b> of <%=rs("college")%>
from the <%=rs("type")%> database.
<p>
<font color="#ff0000">This action is not reversable.</font></font>

<input type="hidden" name="email_name" value="<%=rs("email_name")%>">
<input type="hidden" name="college" value="<%=rs("college")%>">
<input type="submit" value="delete">
</form>

<!-- MODIFY FORM, COMMON FIELDS -------------------------->

<% else %>

<form action="<%=form%>.asp" method="post">

<center>
<table cellspacing=0 cellpadding=3 border=0>

<!-- name -->

<tr>
   <td colspan=2 align="center" <%=light%>>
	<font face="arial"><b>Name</b></font></td>
<tr>
   <td colspan=2 align="center" bgcolor="#C0C0C0">
	
<% if Session("type") = "faculty" then %>	
   <font face="arial" size=1>Title</font>
	<input type="text" name="title" <%if fill=1 then%>value="<%=rs("title")%>"<%end if%> size=4>
<% end if %>

   <font face="arial" size=1>First</font>
	<input type="text" name="name_first" <%if fill=1 then%>value="<%=rs("name_first")%>"<%end if%> size=10>
   <font face="arial" size=1>Last</font>
	<input type="text" name="name_last" <%if fill=1 then%>value="<%=rs("name_last")%>"<%end if%> size=10></td>

<!-- contact information -->

<tr>
   <td colspan=2 align="center" <%=light%>>
	<font face="arial"><b>Contact Information</b></font></td>
<tr>
   <td align="center" colspan=2 bgcolor="#C0C0C0">
	<font face="arial" size=2>
	(208) 364-</font><input type="text" name="phone" <%if fill=1 then%>value="<%=rs("phone")%>"<%end if%> size=4>     
   <input type="text" name="email_name" <%if fill=1 then%>value="<%=rs("email_name")%>"<%end if%> size=10>@<input type="text" name="email_site" <%if fill=1 then%>value="<%=rs("email_site")%>"<%end if%> size=25></td>


<!-- position -->

<tr>
   <td align="RIGHT" <%=light%>>
	<font face="arial" size=2><b>Position</b></font></td>
   <td bgcolor="#C0C0C0">
	<input type="text" name="position" <%if fill=1 then%>value="<%=rs("position")%>"<%end if%> size=40></td>

<!-- college -->

<tr>
   <td align=right <%=light%>>
	<font face="arial" size="2"><b>College</b></font></td>
   <td bgcolor="#C0C0C0">
	<select name="college">
<%
	for each i in college_list
		response.write "<option"
		if college = i then
			response.write " selected"
		end if
		response.write ">" & i & VbCrLf
	next
%>
	</select>
	</td>

<!--- FACULTY MODIFY ------------------------------------->

<%	if Session("type") = "faculty" then %>

<!-- discipline -->

<tr>
   <td align=right <%=light%>>
	<font face="arial" size="2"><b>Discipline</b></font></td>
   <td bgcolor="#C0C0C0"><input type="text" name="area" <%if fill=1 then%>value="<%=rs("area")%>"<%end if%> size=40></td>

<!-- education -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Education</b></font></td>
<tr>
   <td align=center bgcolor="#C0C0C0"><font face="arial" size=1>Degree(s)</font></td>
   <td align=right bgcolor="#C0C0C0"><font face="arial" size=1>
	Location &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; &nbsp; Date &nbsp;</font>
	</td>

<%		for i = 1 to 4 %>

<tr>
   <td colspan=2 align=center bgcolor="#C0C0C0"><b><%=i%></b>
	<input type="text" name="degree<%=i%>_type" <%if fill=1 then%>value="<%=rs("degree"&i&"_type")%>"<%end if%> size=10> 
	<input type="text" name="degree<%=i%>_place" <%if fill=1 then%>value="<%=rs("degree"&i&"_place")%>"<%end if%> size=40> 
	<input type="text" name="degree<%=i%>_date" <%if fill=1 then%>value="<%=rs("degree"&i&"_date")%>"<%end if%> size=4></td>

<%		next %>

<!-- emphasis -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Emphasis</b></font></td>
<tr>
   <td colspan=2 align=center bgcolor="#C0C0C0"><textarea name="emphasis" cols=50 rows=10 wrap="virtual"><%if fill=1 then%><%=rs("emphasis")%><%end if%></textarea></td>

<!-- experience -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Experience</b></font></td>
<tr>
   <td align=center bgcolor="#C0C0C0"><font face="arial" size=1>Time</td>
   <td align=center bgcolor="#C0C0C0"><font face="arial" size=1>Description</td>

<%		for i = 1 to 4 %>

<tr>
   <td bgcolor="#C0C0C0" colspan=2><b><%=i%></b>
	<input type="text" name="exp<%=i%>_time" <%if fill=1 then%>value="<%=rs("exp"&i&"_time")%>"<%end if%> size=20><br>
   <center><textarea name="exp<%=i%>_desc" cols=50 rows=2 wrap="virtual"><%if fill=1 then%><%=rs("exp"&i&"_desc")%><%end if%></textarea></center></td>

<%		next %>

<!-- scholarship -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Scholarship</b></font></td>
<tr>
   <td colspan=2 align=center bgcolor="#C0C0C0">
	<textarea name="scholarship" cols=50 rows=10 wrap="virtual"><%if fill=1 then%><%=rs("scholarship")%><%end if%></textarea></td>

<!-- STAFF MODIFY ---------------------------------------->

<%	elseif Session("type") = "staff" then %>

<!-- duties -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Duties</b></font></td>

<%		for i = 1 to 6 %>

<tr>
   <td colspan=2 align=center bgcolor="#C0C0C0"><b><%=i%></b>
	<input type="text" name="duty<%=i%>" <%if fill=1 then%>value="<%=rs("duty"&i)%>"<%end if%> size=50></td>

<%		next %>

<!-- projects -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Projects</b></font></td>

<%		for i = 1 to 6 %>

<tr>
   <td colspan=2 align=center bgcolor="#C0C0C0"><b><%=i%></b>
	<input type="text" name="project<%=i%>" <%if fill=1 then%>value="<%=rs("project"&i)%>"<%end if%> size=50></td>

<% 	next %>

<!-- COMMON FIELDS --------------------------------------->

<% end if %>

<!-- custom links -->

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Custom Links</b></font></td>

<%		for i = 1 to 6 %>

<tr>
   <td bgcolor="#C0C0C0" colspan=2 align=center><b><%=i%></b>
	<input type="text" name="site<%=i%>_description" <%if fill=1 then%>value="<%=rs("site"&i&"_description")%>"<%end if%> size=15>
   <font face="arial" size=2>http://</font><input type="text" name="site<%=i%>_url" <%if fill=1 then%>value="<%=rs("site"&i&"_url")%>"<%end if%> size=30></td>

<%		next %>

<tr>
   <td colspan=2 align=center <%=light%>>
	<font face="arial"><b>Leave these as is if unsure</b></font></td>

<!-- submit button -->

<tr>
	<td align=right <%=light%>>&nbsp;</td>
	<td bgcolor="#c0c0c0" align=right>
	<input type="submit" value="<%=Request.QueryString("action")%>"></td>

<!-- last updated information -->

<%		if fill = 1 then %>
<tr>
	<td colspan=2 align=center <%=light%>>
	<font size=1 face="arial">
	(Last updated <%=rs("update_time")%> from machine <%=rs("update_machine")%>)
	</font></td>
<%		end if %>

</table>
</form>

<% end if %>

<!--#include virtual="/footer.inc"-->